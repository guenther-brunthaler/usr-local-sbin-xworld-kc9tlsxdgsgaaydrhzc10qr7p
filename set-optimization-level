#! /usr/bin/perl -w
# Adds a list of package descriptions in the format "category/package"
# to the list of heavily optimized packages by creating symlinks
# in /etc/portage/env.
#
# $HeadURL: /caches/xsvn/uxadm/trunk/usr/local/sbin/set-optimization-level $
# $Author: root(xvaio) $
# $Date: 2006-10-14T13:45:58.219943Z $
# $Revision: 337 $


use strict;


my $outdir= '/etc/portage/env';
my $target= '../heavily-optimized';


my($d);
foreach (@ARGV ? @ARGV : <>) {
   s/^\s*|\s*$//g;
   substr($_, 0, 0)= "$outdir/";
   die unless ($d)= m{^(.+)/[^/]+$};
   mkdir $d or die "Cannot mkdir '$d': $!" unless -d $d;
   if (-l) {
      next if readlink eq $target;
      unlink($_) == 1 || die "Cannot remove old '$_': $!";
   } elsif (-e) {
      # Note: '-l' also works for symlinks where '-e' will fail!
      die "'$_' already exists and is not a symlink";
   }
   symlink $target, $_ or die "Cannot symlink '$_': $!";
   print "symlinked '$_'.\n";
}
